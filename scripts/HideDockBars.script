'info:Hide Dockbars#Internal Scripts#Hide dockbars

Call HideDockBars_script

Sub HideDockBars_script
	Dim strDisabledActionsList, strAction, strEnabledActionsList
	Dim arrDisabledActions, arrEnabledActions
	Dim objFrame, objSplitter
	Dim strViewType
	Dim objQuery

	ActionState.TerminateCurrentAction

	Freeze 1, "HideDockBars_script"

	'-----------------------------------------------
	Call HideDockBars_main ("")
	'-----------------------------------------------

	If Data.GetValue("\General\ExistXPBar") Then
		XP_Block
	End If

	If Data.GetValue("\DockBars\DisableActionsForPage") Then	'Блокировка акций по спискам из shell.data //
		'ActionState.ResetAllActionStates
		strDisabledActionsList = Data.GetValue("\DockBars\DisabledActions")
		arrDisabledActions = Split(strDisabledActionsList, vbNewLine)
		For i = 0 To UBound(arrDisabledActions)
			strAction = Trim(Mid(arrDisabledActions(i), 2))
			If strAction <> "" Then
				ActionState.SetActionState strAction, 0
			End If
		Next
		strEnabledActionsList = Data.GetValue( _
		  "\DockBars\EnabledActions" & Data.GetValue("\DockBars\PageType") )
		arrEnabledActions = Split(strEnabledActionsList, vbNewLine)
		For i = 0 To UBound(arrEnabledActions)
			strAction = Trim(Mid(arrEnabledActions(i), 2))
			If strAction <> "" Then
				ActionState.ResetActionState strAction
			End If
		Next
	End If

	' Управление DataContextCtrl-ом //
	If Not Docs.IsPointerNull(DataContextCtrl) Then
		If Data.GetDefValue("\DockBars\ChangeDataContextCtrl", 1) Then DataContextCtrl.SetEnable False
	End If

	'запоминаем состояние сплиттера //
	MethodMan.StoreSplitter "\DockBars\Splitter"

	Data.SetValue "\Form\WasPressOk", 0

	Set objFrame = MainWnd.ActiveFrame
	If TypeName(objFrame) <> "Nothing" Then	'[ay]

		Set objSplitter = objFrame.GetSplitter
		strViewType = objSplitter.GetActiveViewType (col, row)
		Data.SetValue "\General\LastView", strViewType
		Data.SetValue "\General\LastViewCol", col
		Data.SetValue "\General\LastViewRow", row
		Data.SetValue "\General\LastColCount", objSplitter.GetColumnsCount
		Data.SetValue "\General\LastRowCount", objSplitter.GetRowCount
		If strViewType = "DBaseDoc.BlankView" Then			'return active query in DataBase
			Set objQuery = Objects.GetActiveObject ("Query")
			Data.SetValue "\General\LastUsedQuery", objQuery.Name
			Set objQuery = Nothing
		End If
		
		'блокировка кнопок сплиттера при вызове формы //
		'для разблокировки на конкретной форме можно использовать BlockSplitterButtons(0) //
		objSplitter.BlockButton True
		objSplitter.EnableButton -1, -1, 31, 0

		Data.SetValue "\DockBars\Splitter\MergeSmall", objSplitter.MergeSmall
		objSplitter.MergeSmall = False	'отмена разделения окна путем перетаскивания сплиттера к краю //

		Set objSplitter = Nothing

	End If
	Set objFrame = Nothing

	'включаем синхронизацию содержания для форм, работающих с разделенным окном //
	If Data.GetDefValue("\DockBars\UseSyncContext", 1) Then
		Data.SetValue "\DockBars\ActionStates\SyncContextAll", ActionState.GetActionState("SyncContextAll")
		Call WindowUnSplit
		SyncContextAll 1
	End If
	'запоминаем синхронизацию увеличения //
	Data.SetValue "\DockBars\ActionStates\SyncZoom", ActionState.GetActionState("SyncZoom")

	MainWnd.IdleUpdate	'Прорисовка кнопок после изменения их ActionState //

	Freeze 0, "HideDockBars_script"
End Sub
