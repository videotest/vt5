'resource:Input Device X/Y Ratio...#Measurement#Specify X/Y pixel ratio for active input device#Input Device X/Y Ratio#Measurement
'state:function=InputAvail

Call RunCalibrXYRatio_script

Sub RunCalibrXYRatio_script
	Dim sMsg1
	Dim ShowContoursOld, ShowChildObjectsOld

	sMsg1 = Docs.LoadString ("Place stage-micrometer vertically,\nand acquire image.")
	sMsg2 = Docs.LoadString ("Place stage-micrometer horizontally,\nand acquire image.")
	sMsg3 = Docs.LoadString ("Acquire image of object with equal X/Y dimensions\n (square, circle) to calibrate X/Y ratio of input system.")

	g_bSkipActionsEvent = True

	'Включаем показ контуров и изм. линий, если они выключены //
	ShowContoursOld = Data.GetValue ("\ImageView\ShowContours")
	ShowChildObjectsOld = Data.GetValue ("\ImageView\ShowChildObjects")
	Data.SetValue "\ImageView\ShowContours", 1
	Data.SetValue "\ImageView\ShowChildObjects", 1

	Data.SetValue "\SelectOperation\Info", Docs.LoadString ("Select test object to calibrate input system:")
	Data.SetValue "\SelectOperation\OperationsCount", 2
	Data.SetValue "\SelectOperation\Operation_1", Docs.LoadString ("stage-micrometer")
	Data.SetValue "\SelectOperation\Operation_2", Docs.LoadString ("circle, square")
	sCommentText = Docs.LoadString ("Input device:") & " " & _
	  Data.GetValue("\Input\Device") & vbNewLine & _
	  Docs.LoadString ("Current X/Y ratio:") & " " & _
	  FormatNumber( Data.GetDefValue("\Input\" & _
	  Data.GetDefValue ("\Input\Device", "Open File") & "\PixelAspectRatio", 1), 3 )
	Data.SetValue "\SelectOperation\Comment", sCommentText
	
	FormManager.ExecuteModal "SelectOperationComment"

	Select Case Data.GetValue("\SelectOperation\Result")
	  Case 1
		MsgBox sMsg1, vbInformation, Data.GetDefValue("\General\DefaultTitle", "")
		If Docs.IsPointerNull( Docs.GetActiveDocument("Image") ) Then
			bLoad = ActionManager.InputImage(0,1,1)
		Else
			bLoad = ActionManager.InputImageInCurrentDoc(0,1,1)
		End If

		If bLoad = 1 Then

			HideDockBars

			Data.SetValue "\tmp\CalibrationStep", "mic1"
			FormManager.ExecutePageModal "NewCalibrXYRatio"

			If Data.GetValue ("\tmp\FormOK") = 1 Then
			
				MsgBox sMsg2, vbInformation, Data.GetDefValue("\General\DefaultTitle", "")
				bLoad = ActionManager.InputImageInCurrentDoc(0,1,1)

				If bLoad = 1 Then

					Data.SetValue "\tmp\CalibrationStep", "mic2"
					FormManager.ExecutePageModal "NewCalibrXYRatio"

					If Data.GetValue ("\tmp\FormOK") Then Call XYImageCorrection

				End If
	
			End If

			ShowDockBars

		End If

	  Case 2
		MsgBox sMsg3, vbInformation, Data.GetDefValue("\General\DefaultTitle", "")
		If Docs.IsPointerNull( Docs.GetActiveDocument("Image") ) Then
			bLoad = ActionManager.InputImage(0,1,1)
		Else
			bLoad = ActionManager.InputImageInCurrentDoc(0,1,1)
		End If

		If bLoad <> 0 Then
			Data.SetValue "\ImageView\ShowChildObjects", 0
			MainWnd.UpdateSystemSettingsForCurrentDoc

			HideDockBars

			Data.SetValue "\tmp\CalibrationStep", "circle"
			FormManager.ExecutePageModal "NewCalibrXYRatio"

			ShowDockBars

			If Data.GetValue ("\tmp\FormOK") Then Call XYImageCorrection
		End If

	End Select

	g_bSkipActionsEvent = False

	'Восстанавливаем показ контуров и изм. линий //
	Data.SetValue "\ImageView\ShowContours", ShowContoursOld
	Data.SetValue "\ImageView\ShowChildObjects", ShowChildObjectsOld
	MainWnd.UpdateSystemSettingsForCurrentDoc

End Sub
