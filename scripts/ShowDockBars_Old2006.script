'info:Show Dockbars#Internal Scripts#Show dockbars

Call ShowDockBars_script

Sub ShowDockBars_script
	Dim strDisabledActionsList, strAction
	Dim arrDisabledActions
	Dim objFrame, objSplitter
	Dim strViewType

	Freeze &H0, "ShowDockBars_script"

	'восстанавливаем синхронизацию для форм, работающих с разделенным окном //
	If Data.GetDefValue("\DockBars\UseSyncContext", 1) Then
		If ( Data.GetDefValue("\DockBars\ActionStates\SyncContextAll", 0) And 2 ) Then
			SyncContextAll 1
		Else
			SyncContextAll 0
		End If
	End If
	Data.SetValue "\DockBars\UseSyncContext", 1

	'восстанавливаем синхронизацию увеличения //
	If ( Data.GetDefValue("\DockBars\ActionStates\SyncZoom", 0) And 2 ) Then
		SyncZoom 1
	Else
		SyncZoom 0
	End If

	strViewType = Data.GetValue ("\General\LastView")

	Set objFrame = MainWnd.ActiveFrame
	If TypeName(objFrame) <> "Nothing" Then		'[ay]
		Set objSplitter = objFrame.GetSplitter

'		========== раскомментарить при отказе от MethodMan.LoadSplitter ==========
'		'Восстанавливаем деление окна как было до вызова формы //
'		If Data.GetDefValue("\DockBars\RestoreWindowSplit", 0) Then _
'		  objSplitter.Split Data.GetDefValue ("\General\LastColCount", 1), _
'		  Data.GetDefValue ("\General\LastRowCount", 1)
'
'		If strViewType <> "" Then
'			If Data.GetValue("\General\LastViewCol") = 0 And _
'			  Data.GetValue("\General\LastViewRow") = 0 Then
'				objSplitter.SetViewType 0, 0, strViewType
'			End If
'		End If
		
		If Left(strViewType, 9) = "DBaseDoc." Then		'return active query in DataBase
			Call SetView(strViewType)
			ObjectSelect Data.GetValue ("\General\LastUsedQuery")
		End If

		'отмена блокировки кнопок сплиттера при вызове формы //
		objSplitter.BlockButton False
		objSplitter.EnableButton -1, -1, 31, 31

		'восстанавливаем разделения окна путем перетаскивания сплиттера к краю //
		objSplitter.MergeSmall = Data.GetDefValue ("\DockBars\Splitter\MergeSmall", 0)

		Set objSplitter = Nothing

	End If
	Set objFrame = Nothing

	'восстанавливаем состояние сплиттера //
	'если на форме нажали ок - для активного вью ничего не надо восстанавливать //
	If Data.GetDefValue ("\Form\WasPressOk", 0) Then
		MethodMan.StoreSplitter "\tmp\Splitter"
		nRow = Data.GetDefValue("\tmp\Splitter\ActiveRow")
		nCol = Data.GetDefValue("\tmp\Splitter\ActiveCol")
		sViewSec = "\tmp\Splitter\View_" & nRow & "_" & nCol & "\"
		Data.RemoveValue sViewSec & "ViewZoom"
		Data.RemoveValue sViewSec & "ViewPosX"
		Data.RemoveValue sViewSec & "ViewPosY"

		nRow = Data.GetDefValue("\DockBars\Splitter\ActiveRow")
		nCol = Data.GetDefValue("\DockBars\Splitter\ActiveCol")
		sViewSec2 = "\DockBars\Splitter\View_" & nRow & "_" & nCol & "\"

		Call save_entry_ex (Data, sViewSec, sViewSec2)
		Data.RemoveValue "\tmp\Splitter\"
	End If
	MethodMan.LoadSplitter "\DockBars\Splitter"

	'-----------------------------------------------
	Call ShowDockBars_main ("")
	'-----------------------------------------------

	If Data.GetValue("\DockBars\DisableActionsForPage") Then	'Снятие блокировки акций //
		strDisabledActionsList = Data.GetValue("\DockBars\DisabledActions")
		arrDisabledActions = Split(strDisabledActionsList, vbNewLine)
		For i = 0 To UBound(arrDisabledActions)
			strAction = Trim(Mid(arrDisabledActions(i), 2))
			If strAction <> "" Then
				If ActionState.GetActionState(strAction) = 0 Then
					ActionState.ResetActionState strAction
				End If
			End If
		Next
		Data.SetValue "\DockBars\PageType", ""
	End If

	' Управление DataContextCtrl-ом //
	If Not Docs.IsPointerNull(DataContextCtrl) Then
		If Data.GetDefValue("\DockBars\ChangeDataContextCtrl", 1) Then DataContextCtrl.SetEnable True
		Data.SetValue "\DockBars\ChangeDataContextCtrl", 1
	End If


	If Data.GetValue ("\General\ExistXPBar") Then
		XP_UnBlock
	End If

	MainWnd.IdleUpdate	'Прорисовка кнопок после изменения их ActionState //

	Freeze 0, "ShowDockBars_script"
End Sub
